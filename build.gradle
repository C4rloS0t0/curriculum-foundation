import org.asciidoctor.gradle.AsciidoctorTask
import javax.inject.Inject
import java.text.SimpleDateFormat

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.asciidoctor:asciidoctorj-pdf:1.5.3"
  }
}
plugins {
  id "org.asciidoctor.convert" version "1.6.0"
}

ext {
  today = new Date()
  versionDate = new SimpleDateFormat("yyyyMMdd").format(today)

  // read document version from file "document.version"
  project.version = project.file("./document.version").text

  validity = new Date(project.file("./document.validity").text.trim())
  validityEnglish = "valid from " + new SimpleDateFormat("MMMM d, yyyy", Locale.US).format(validity)
  validityGerman = "GÃ¼ltig ab " + new SimpleDateFormat("d. MMMM yyy", Locale.GERMANY).format(validity)

  curriculumBaseName = "curriculum-foundation"
  addSuffixToCurriculum = { suffix ->
    for (extension in ["html", "pdf"]) {
      File source = new File("${buildDir}/${curriculumBaseName}.${extension}")
      File target = new File("${buildDir}/${curriculumBaseName}${suffix}.${extension}")

      source.renameTo(target)
    }
  }
}

class RenderCurriculumTask extends AsciidoctorTask {
  @Inject
  RenderCurriculumTask(String curriculumBaseName, String versionDate, String validity, String language, boolean withRemarks) {
    sourceDir = new File("./docs/")
    sources {
      include "index.adoc"
      include "${curriculumBaseName}.adoc"
    }
    outputDir = new File("./build/")
    separateOutputDirs = false
    backends 'pdf', 'html5'

    def fileVersion = project.version.trim() + "-" + language

    attributes = [
      'icons'            : 'font',
      'revnumber'        : fileVersion,
      'version-label'    : '',
      'revdate'          : versionDate,
      'currentDate'      : versionDate,
      'language'         : language,
      'withRemarks'      : withRemarks,
      'document-version' : fileVersion + "-" + versionDate + " (" + validity + ")",
      'debug_adoc'       : false,
      'pdf-stylesdir'    : '../pdf-theme/themes',
      'pdf-fontsdir'     : '../pdf-theme/fonts',
      'pdf-style'        : 'isaqb',
      'with-uniqe-LG-id' : false,
      'allow-uri-read'   : true,
      'outputDir'        : outputDir
    ]
  }
}

task buildDocs {
  group 'Documentation'
  description 'Grouping task for generating all languages in several formats'
  dependsOn "includeLearningObjectives", "renderNoRemarksDE", "renderNoRemarksEN", "renderWithRemarksDE_EN"
}

task renderNoRemarksDE(type: RenderCurriculumTask,
    constructorArgs: [curriculumBaseName, versionDate, validityGerman,  "DE", false]) {
  group 'Documentation'
  description 'Builds the German curriculum as PDF and HTML file, without remarks.'
  doLast {
    addSuffixToCurriculum("-de")
  }
}

task renderNoRemarksEN(type: RenderCurriculumTask,
    constructorArgs: [curriculumBaseName, versionDate, validityEnglish, "EN", false]) {
  group 'Documentation'
  description 'Builds the English curriculum as PDF and HTML file, without remarks.'
  doLast {
    addSuffixToCurriculum("-en")
  }
}

task renderWithRemarksDE_EN(type: RenderCurriculumTask,
    constructorArgs: [curriculumBaseName, versionDate, validityEnglish, "DE;EN", true]) {
  group 'Documentation'
  description 'Builds both the German and the English curricula as PDF and HTML files, with remarks. Those files are for internal use only.'
  doLast {
    addSuffixToCurriculum("-remarks-de-en")
  }
}


apply from: 'scripts/includeLearningObjectives.gradle'

defaultTasks "buildDocs"
