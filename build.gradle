import org.asciidoctor.gradle.AsciidoctorTask
import javax.inject.Inject
import java.text.SimpleDateFormat

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.asciidoctor:asciidoctorj-pdf:1.5.0-beta.2"
  }
}
plugins {
  id "org.asciidoctor.convert" version "1.6.0"
}

ext {
  today = new Date()
  englishDate = new SimpleDateFormat("MMMM d, yyyy", Locale.US).format(today)
  germanDate = new SimpleDateFormat("d. MMMM yyy", Locale.GERMANY).format(today)

  // read document version from file "document.version"
  project.version = project.file("./document.version").text
  curriculumBaseName = "foundation-curriculum"
  addSuffixToCurriculum = { suffix ->
    for (extension in ["html", "pdf"]) {
      File source = new File("${buildDir}/${curriculumBaseName}.${extension}")
      File target = new File("${buildDir}/${curriculumBaseName}${suffix}.${extension}")

      source.renameTo(target)
    }
  }
}

class RenderCurriculumTask extends AsciidoctorTask {
  @Inject
  RenderCurriculumTask(String curriculumBaseName, String currentDate, String language, boolean withRemarks) {
    sourceDir = new File("./docs/")
    sources {
      include "index.adoc"
      include "${curriculumBaseName}.adoc"
    }
    outputDir = new File("./build/")
    separateOutputDirs = false
    backends 'pdf', 'html5'

    def fileVersion = project.file("./document.version").text.trim() + "-" + language

    attributes = [
      'icons'            : 'font',
      'revnumber'        : fileVersion,
      'revdate'          : currentDate,
      'currentDate'      : currentDate,
      'language'         : language,
      'withRemarks'      : withRemarks,
      'document-version' : fileVersion + " (" + currentDate + ")",
      'debug_adoc'       : false,
      'pdf-stylesdir'    : '../pdf-theme/themes',
      'pdf-fontsdir'     : '../pdf-theme/fonts',
      'pdf-style'        : 'isaqb',
      'with-uniqe-LG-id' : false,
      'allow-uri-read'   : true,
      'outputDir'        : outputDir
    ]
  }
}

task buildDocs {
  group 'Documentation'
  description 'Grouping task for generating all languages in several formats'
  dependsOn "includeLearningObjectives", "renderNoRemarksDE", "renderNoRemarksEN", "renderWithRemarksDE_EN"
}

task renderNoRemarksDE(type: RenderCurriculumTask,
    constructorArgs: [curriculumBaseName, germanDate, "DE", false]) {
  group 'Documentation'
  description 'Builds the German curriculum as PDF and HTML file, without remarks.'
  doLast {
    addSuffixToCurriculum("-de")
  }
}

task renderNoRemarksEN(type: RenderCurriculumTask,
    constructorArgs: [curriculumBaseName, englishDate, "EN", false]) {
  group 'Documentation'
  description 'Builds the English curriculum as PDF and HTML file, without remarks.'
  doLast {
    addSuffixToCurriculum("-en")
  }
}

task renderWithRemarksDE_EN(type: RenderCurriculumTask,
    constructorArgs: [curriculumBaseName, germanDate, "DE;EN", true]) {
  group 'Documentation'
  description 'Builds both the German and the English curricula as PDF and HTML files, with remarks. Those files are for internal use only.'
  doLast {
    addSuffixToCurriculum("-remarks-de-en")
  }
}

task publish(type: GradleBuild) {
  String travisPr = System.getenv('TRAVIS_PULL_REQUEST')
  if ("false".equals(travisPr)) {
      buildFile = 'publish.gradle'
      tasks = ['gitPublishPush']
  } else {
      println 'env var TRAVIS_PULL_REQUEST says we build for PR #' + travisPr + ', won\'t publish docs!'
  }
}

apply from: 'scripts/includeLearningObjectives.gradle'

defaultTasks "buildDocs"
