import static groovy.io.FileType.*
import java.util.regex.*

class LearningObjective {
    String title
    int chapter
    int chapterIndex

    LearningObjective(String title, String chapter, String chapterIndex) {
      this.title = title
      this.chapter = Integer.valueOf(chapter)
      this.chapterIndex = Integer.valueOf(chapterIndex)
    }
}

task includeLearningObjectives(
        description: 'collect learning objectives from source files and include them as listing',
        group: 'isaqb-curriculum'
) {
    doFirst {
        buildDir.mkdirs()
    }

    doLast {
        def contentDE = createLearningObjectivesContent("DE", ~/==== (LZ (\d*)-(\d*).*)/, "Verzeichnis der Lernziele")
        def contentEN = createLearningObjectivesContent("EN", ~/==== (LG (\d*)-(\d*).*)/, "List of Learning Goals")

        writeLearningObjectives(contentDE, contentEN)
    }

    ext.createLearningObjectivesContent = { language, pattern, headline ->
        def learningObjectives = collectLearningObjectives(pattern)

        sortLearningObjectives(learningObjectives)

        return compileLearningObjectives(language, headline, learningObjectives)
    }

    ext.collectLearningObjectives = { pattern ->
        def docsFolder = new File(projectDir, '/docs')
        def learningObjectives = []
        docsFolder.traverse(type: FILES) { file ->
            if(file.name ==~ '.*[.](ad|adoc|asciidoc)$') {
                def content = file.text
                def matcher = content =~ pattern

                while(matcher) {
                    def title = matcher.group(1)
                    def chapter = matcher.group(2)
                    def chapterIndex = matcher.group(3)
                    learningObjectives.add(
                        new LearningObjective(title, chapter, chapterIndex))
                }
            }
        }

        return learningObjectives
    }

    ext.sortLearningObjectives =  { learningObjectives ->
        learningObjectives.sort { a, b ->
            if(a.chapter == b.chapter) {
                a.chapterIndex <=> b.chapterIndex
            } else {
                a.chapter <=> b.chapter
            }
        }
    }

    ext.compileLearningObjectives = { language, headline, learningObjectives ->
        def content = ''<<''

        content <<= "// tag::" + language + "[]\n"
        content <<= "== " + headline + "\n\n"

        learningObjectives.each { learningObjective ->
            content <<= "- " + learningObjective.title + "\n"
        }

        content <<= "// end::" + language + "[]\n"

        return content.toString()
    }

    ext.writeLearningObjectives = { contentDE, contentEN ->
        def outFile = new File(buildDir, '/learning-objectives.adoc')

        outFile.withWriter('UTF-8') { writer ->
            writer.writeLine("// this is autogenerated\n")
            writer.writeLine(contentDE)
            writer.writeLine(contentEN)
        }
    }
}
